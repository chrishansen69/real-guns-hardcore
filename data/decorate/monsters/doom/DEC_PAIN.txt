actor PainElemental_2 : RGAMonster
{
  health 400
  radius 31
  height 56
  mass 400
  speed 8
  painchance 128
  seesound "pain/sight"
  painsound "pain/pain"
  deathsound "pain/death"
  activesound "pain/active"
  MONSTER
  +FLOAT
  +NOGRAVITY
  states
  {
  Spawn:
    POSS A 0 
    POSS A 0 Thing_SetSpecial(0,226,613,0,random(12,24))
    PAIN A 10 A_Look
    goto Spawn+2
  See:
    PAIN AABBCC 3 A_Chase
    loop
  Missile:
    PAIN DE 5 A_FaceTarget
    PAIN F 5 bright A_FaceTarget
    PAIN F 0 bright A_Jump(128,2,3)
    PAIN F 0 bright A_PainAttack("LostSoul_2")
    goto See
    PAIN F 0 bright A_PainAttack("SpectreLostSoul")
    goto See
    PAIN F 0 bright A_PainAttack("KamikazeSoul")
    goto See
  Pain:
    PAIN G 6
    PAIN G 6 A_Pain
	TNT1 G 0 A_AlertMonsters
    goto See
  Death:
    PAIN H 8 bright
    PAIN I 8 bright A_Scream
    PAIN JK 8 bright
    PAIN F 0 bright A_Jump(128,3,4)
    PAIN L 8 bright A_PainDie("LostSoul_2")       // See LostSoul
    PAIN M 8 bright A_NoBlocking
    TNT1 A -1 
    stop
    PAIN L 8 bright A_PainAttack("SpectreLostSoul")
    goto Death+6
    PAIN L 8 bright A_PainAttack("KamikazeSoul")
    goto Death+6
  Raise:
    PAIN MLKJIH 8
    goto See
  }
}

actor SpectrePainElemental : PainElemental_2 
{
  renderstyle OptFuzzy
  alpha 0.2
  +SHADOW
  health 500
  states
  {
  Spawn:
    POSS A 0 
    POSS A 0 Thing_SetSpecial(0,226,613,0,random(15,30))
    PAIN A 10 A_Look
    goto Spawn+2
  See:
    PAIN AABBCC 3 A_Chase
    loop
  Missile:
    PAIN DE 5 A_FaceTarget
    PAIN F 5 bright A_FaceTarget
    PAIN F 0 bright A_PainAttack("SpectreLostSoul")
    goto See
  Pain:
    PAIN G 6
    PAIN G 6 A_Pain
	TNT1 G 0 A_AlertMonsters
    goto See
  Death:
    PAIN H 8 bright
    PAIN I 8 bright A_Scream
    PAIN JK 8 bright
    PAIN L 8 bright A_PainDie("SpectreLostSoul")       // See LostSoul
    PAIN M 8 bright A_NoBlocking
    TNT1 A -1 
    stop
  Raise:
    PAIN MLKJIH 8
    goto See
  }
}

actor KamikazePainElemental : PainElemental_2 
{
  obituary "%o was imploded by a lost soul."
  health 500
  bloodcolor "Blue"
  states
  {
  Spawn:
    POSS A 0 
    POSS A 0 Thing_SetSpecial(0,226,613,0,random(15,30))
    KPEL A 10 A_Look
    goto Spawn+2
  See:
    KPEL AABBCC 3 A_Chase
    loop
  Missile:
    KPEL DE 5 A_FaceTarget
    KPEL F 5 bright A_FaceTarget
    KPEL F 0 bright A_PainAttack("KamikazeSoul")
    goto See
  Pain:
    KPEL G 6
    KPEL G 6 A_Pain
	TNT1 G 0 A_AlertMonsters
    goto See
  Death:
    KPEL H 8 bright
    KPEL I 8 bright A_Scream
    PAIN L 8 bright A_PainDie("KamikazeSoul")       // See LostSoul
    KSKL FF 0 A_CustomMissile("KamikazeExplosion",28,0,random(-180,180),2,random(-36,36))
    KSKL F 0 A_Stop
    TNT1 AAAAAAAAAAAAA 8 A_SpawnItemEx("WeaponSmokePuffLong",0,0,0,0,0,1,0,128,0)
    TNT1 A -1 A_NoBlocking
    stop
  Raise:
    TNT1 A 0
    TNT1 A 0 A_SpawnItemEx("KamikazePainFade", 0, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 AAAAAAAAAA 2 
    goto See
  }
}

actor KamikazePainFade
{
  RenderStyle Translucent
  alpha 0.0
  +RANDOMIZE
  +NOCLIP
  +NOGRAVITY
  Scale 1.0
  states
  {
  Spawn:
    KPEL H 0
    KPEL HHHHHHHHHH 2 A_FadeIn(0.1)
    stop
  }
}
