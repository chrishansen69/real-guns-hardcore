actor ZombieMan_2 : RGAMonster
{
	Tag "Pistol Zombie"
  spawnid 4
  obituary "%o was killed by a zombieman."
  health 40
  radius 20
  height 56
  mass 100
  speed 11
  cameraheight 48
  painchance 200
  seesound "grunt/sight"
  attacksound "u45/fire"
  painsound "grunt/pain"
  deathsound "grunt/death"
  activesound "grunt/active"
  DropItem "HandgunSpawner" 128
  DropItem "HandgunAmmo" 255
  DropItem "HandgunAmmo" 128
  GibHealth 5
  MONSTER
  +FLOORCLIP
  +MISSILEEVENMORE
  states
  {
  Spawn:
    POSS A 0 
    POSS A 0 A_GiveInventory("FragGrenadeAmmo",1)
    POSS A 0 Thing_SetSpecial(0,226,613,0,random(5,10))
    POSS A 0 A_GiveInventory("XPAmount", random(5,10))
    POSS P 0 ACS_ExecuteAlways(651,0,0,0,0)
    POSS AB 10 A_Look
    goto Spawn+5
  See:
    POSS AABBCCDD 3 A_Chase
    loop
  Missile:
    POSS E 5 A_FaceTarget
    POSS E 0 A_Jump(20,"Grenade")
    POSS F 4 A_CustomBulletAttack(22.5, 0, 1, 3, "BulletPuff")
    POSS E 4
    goto See
  Grenade:
    POSS E 0 A_JumpIfCloser(512,1)
    goto Missile+2
    POSS E 0 A_JumpIfInventory("FragGrenadeAmmo",1,1)
    goto Missile+2
    POSS E 0 A_PlaySound("russian/grenade")
    POSS E 25 A_PlayWeaponSound("grenade/pinpull")
    POSS E 0 A_FaceTarget
    POSS E 1 A_PlayWeaponSound("grenade/throw")
    POSS E 0 A_TakeInventory("FragGrenadeAmmo",1)
    POSS V 25 A_CustomMissile("FragGrenade_Zombie",30,0,0,2,10)
    goto See
  Pain:
    POSS G 3
    POSS G 3 A_Pain
	TNT1 G 0 A_AlertMonsters
    goto See
  Death:
    POSS H 0
	POSS H 0 A_JumpIfHealthLower(random(-500,-110),"XDeath")
    POSS I 5 A_Scream
    POSS J 5 A_NoBlocking
    POSS J 0 ACS_ExecuteAlways(648,0,0,0,0)
    POSS KK 2 A_SpawnItemEx("HealthBonus",0,0,0,random(-2,2),random(-2,2),random(1,4),0,0,0)
    POSS L -1
    stop
  XDeath:
    POSS M 5
    POSS N 5 A_XScream
    POSS O 5 A_NoBlocking
    POSS P 0 ACS_ExecuteAlways(648,0,0,0,0)
    POSS PQRST 5
    POSS U -1
    stop
  Raise:
    POSS K 1 A_GiveInventory("BlueArmor",200)
    POSS KJIH 5
    goto See
  }
}
actor ZombieSniper : RGAMonster
{
  spawnid 4
  obituary "%o was sniped by a zombieman."
  health 40
  radius 20
  height 56
  mass 100
  speed 8
  painchance 200
  seesound "grunt/sight"
  painsound "grunt/pain"
  deathsound "grunt/death"
  activesound "grunt/active"
  DropItem "SniperRifleSpawner" 15
  DropItem "SniperAmmo" 160
  DropItem "SniperAmmo" 30
  DropItem "HandgunAmmo" 128
  MONSTER
  +FLOORCLIP
  +MISSILEEVENMORE
  states
  {
  Spawn:
    ZHRT A 0 
    ZHRT A 0 A_GiveInventory("FragGrenadeAmmo",1)
    ZHRT A 0 A_GiveInventory("XPAmount",random(5, 10))
	ZHRT A 0 Thing_SetSpecial(0,226,613,0,random(5,10))
    ZHRT AB 10 A_Look
    goto Spawn+4
  See:
    ZHRT AABBCCDD 4 A_Chase
    loop
  Missile:
    ZHRT E 0 A_JumpIfCloser(600,"Pistol")
    ZHRT E 0 A_FaceTarget
    ZHRT E 30 A_FaceTarget
    ZHRT E 0 A_JumpIfCloser(300,"Pistol")
    ZHRT E 0 A_FaceTarget
    ZHRT E 30 A_FaceTarget
    ZHRT E 0 A_PlaySound("barrett/fire", CHAN_WEAPON, 1.0, false, 0.6)
    ZHRT E 0 A_Jump(83,3)
    ZHRT F 10 BRIGHT A_CustomBulletAttack(0.8, 0.8, 1, 35, "SniperPuff", 9999)
    ZHRT E 0 A_Jump(256,2)
    ZHRT F 10 BRIGHT
    ZHRT E 0 A_Jump(30, "See")
    ZHRT E 0 A_SpidRefire
    goto Missile+3
  Pistol:
    ZHRT E 10 A_FaceTarget
    ZHRT F 8 A_CustomBulletAttack(22.5, 0, 1, 4, "BulletPuff")
    ZHRT E 0 A_PlaySound("u45/fire")
    ZHRT E 8
    goto See
  Pain:
    ZHRT G 3
    ZHRT G 3 A_Pain
    goto See
  Death:
    ZHRT H 0
	ZHRT H 0 A_JumpIfHealthLower(random(-500,-110),"XDeath")
    ZHRT I 5 A_Scream
    ZHRT J 5 A_NoBlocking
    ZHRT J 0 ACS_ExecuteAlways(648,0,0,0,0)
    ZHRT KK 2 A_SpawnItemEx("HealthBonus",0,0,0,random(-2,2),random(-2,2),random(1,4),0,0,0)
    ZHRT L -1
    stop
  XDeath:
    ZHRT O 5
    ZHRT P 5 A_XScream
    ZHRT Q 5 A_NoBlocking
    ZHRT Q 0 ACS_ExecuteAlways(648,0,0,0,0)
    ZHRT RTUV 5
    ZHRT W -1
    Stop
  Raise:
    ZHRT K 1 A_GiveInventory("BlueArmor",200)
    ZHRT KJIH 5
    goto See
  }
}
