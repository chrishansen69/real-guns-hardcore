actor HellKnight_2 : RGAMonster
{
  spawnid 113
  obituary "%o was splayed by a Hell Knight."
  hitobituary "%o was gutted by a Hell Knight."
  health 600
  radius 24
  height 64
  mass 1000
  speed 8
  painchance 50
  seesound "knight/sight"
  painsound "knight/pain"
  deathsound "knight/death"
  activesound "knight/active"
  MONSTER
  +FLOORCLIP
  Species "Goat"
  bloodcolor "Green"
  DamageFactor "BaronBall", 0.0
  states
  {
  Spawn:
    POSS A 0 
    POSS A 0 Thing_SetSpecial(0,226,613,0,random(20,40))
    BOS2 AB 10 A_Look
    goto Spawn+2
  See:
    BOS2 AABBCCDD 3 A_Chase
    loop
  Melee:
  Missile:
    BOS2 EF 8 A_FaceTarget
    BOS2 G 8 A_CustomComboAttack("BaronBall_2",32,(5*random(1,5)),"skeleton/melee")
    goto See
  Pain:
    BOS2 H 2
    BOS2 H 2 A_Pain
	TNT1 G 0 A_AlertMonsters
    goto See
  Death:
    BOS2 I 8
    BOS2 J 8 A_Scream
    BOS2 K 8
    BOS2 L 8 A_NoBlocking
    BOS2 MN 8
    BOS2 O -1 A_BossDeath
    stop
  Raise:
    BOS2 ONMLKJI 8
    goto See
  }
}

actor BaronBall_2: BaronBall
{
	Speed 25
}

actor SpectreHellKnight : HellKnight_2
{
  renderstyle OptFuzzy
  alpha 0.2
  +SHADOW
}

actor StoneKnight : HellKnight_2
{
  spawnid 113
  obituary "%o was destroyed by a Stone Knight."
  hitobituary "%o was smashed up by a Stone Knight."
  health 1000
  radius 24
  height 64
  mass 1000
  speed 8
  painchance 5
  seesound "stoneknight/sight"
  painsound "stoner/sight"
  deathsound "stoner/sight1"
  activesound "stoner/sight"
  MONSTER
  +FLOORCLIP
  Species "Goat"
  +NOBLOOD
  +GHOST
  DamageFactor "BaronBall", 0.0
  states
  {
  Spawn:
    POSS A 0 
    POSS A 0 Thing_SetSpecial(0,226,613,0,random(30,50))
    BOS9 AB 10 A_Look
    goto Spawn+2
  See:
    BOS9 AABBCCDD 3 A_Chase
    loop
  Melee:
    BOS9 EF 6 A_FaceTarget
    BOS9 G 6 A_CustomComboAttack("BlueBaronBall",32,(5*random(1,5)),"skeleton/melee")
    BOS9 PQ 6 A_FaceTarget
    BOS9 R 6 A_CustomComboAttack("BlueBaronBall",32,(5*random(1,5)),"skeleton/melee")
    goto See
  Missile:
    BOS9 E 0 A_Jump(32,"Missile2")
    BOS9 EF 6 A_FaceTarget
    BOS9 G 6 A_CustomComboAttack("BlueBaronBall",32,(5*random(1,5)),"skeleton/melee")
    BOS9 PQ 6 A_FaceTarget
    BOS9 R 6 A_CustomComboAttack("BlueBaronBall",32,(5*random(1,5)),"skeleton/melee")
    goto See
  Missile2:
    BOS9 EF 6 A_JumpIfCloser(96,"Melee")
    BOS9 G 0 A_PlayWeaponSound("brain/spit")
    BOS9 G 6 A_CustomMissile("StoneKnightCube",32,0,-24,CMF_AIMDIRECTION,0)
    BOS9 PQ 6 A_JumpIfCloser(96,"Melee")
    BOS9 R 0 A_PlayWeaponSound("brain/spit")
    BOS9 R 6 A_CustomMissile("StoneKnightCube",32,0,24,CMF_AIMDIRECTION,0)
    BOS9 AABBCCDD 3 A_Chase("Melee","")
    BOS9 AABBCCDD 3 A_Chase("Melee","")
    goto See
  Pain:
    BOS9 H 2
    BOS9 H 2 A_Pain
	TNT1 G 0 A_AlertMonsters
    goto See
  Death:
    BOS9 I 8
    BOS9 J 8 A_Scream
    BOS9 K 8
    BOS9 L 8 A_NoBlocking
    BOS9 M 8 
    BOS9 N 8 A_Jump(48,7)
    BOS9 OOOOO 2 A_SpawnItemEx("HealthBonus",0,0,0,random(-2,2),random(-2,2),random(1,4),0,0,0)
    BOS9 O -1 
    stop
    BOS9 OOOOO 115
    BOS9 O 0 A_PlayWeaponSound("brain/spit")
    BOS9 O -1 A_CustomMissile("StoneKnightCubeSelf",8,0,0,CMF_AIMDIRECTION,90)
    stop
  Death.Telefrag:
    BOS9 I 8
    BOS9 J 8 A_Scream
    BOS9 K 8
    BOS9 L 8 A_NoBlocking
    BOS9 M 8 
    BOS9 N 8 A_Jump(192,7)
    BOS9 OOOOO 2 A_SpawnItemEx("HealthBonus",0,0,0,random(-2,2),random(-2,2),random(1,4),0,0,0)
    BOS9 O -1 
    stop
    BOS9 OOOOO 115
    BOS9 O 0 A_PlayWeaponSound("brain/spit")
    BOS9 O -1 A_CustomMissile("StoneKnightCubeSelf",8,0,0,CMF_AIMDIRECTION,90)
    stop
  Raise:
    BOS9 ONMLKJI 8
    goto See
  }
}
actor BlueBaronBall
{
  Radius 6
  Height 16
  Speed 22
  FastSpeed 20
  Damage (20)
  RenderStyle Add
  Alpha 1
  SeeSound "baron/attack"
  DeathSound "baron/shotx"
  Decal "BaronScorch"
  Projectile
  Translation "112:127=[0,0,255]:[0,0,0]"
  states
  {
  Spawn:
    BAL7 AB 4 bright
    loop
  Death:
    BAL7 CDE 6 bright
    stop
  }
}

actor StoneKnightCube
{
  Radius 16
  Height 16
  Speed 10
  Damage (1)
  RenderStyle Normal
  Alpha 1
  SeeSound "brain/cube"
  DeathSound "misc/teleport"
  Projectile
  +RIPPER
  scale 0.5
  reactiontime 12
  +THRUGHOST
  +HEXENBOUNCE
  states
  {
  Spawn:
    BOSF ABCD 3 bright A_Countdown
    loop
  Death:
    TNT1 A 0 A_SpawnItemEx("SpawnFire",0,0,0,0,0,0,0,0,0)
    TNT1 A 0 A_Jump(96,"SpawnRevenant")
    TNT1 A 0 A_Jump(64,"SpawnDemon")
    TNT1 A 0 A_Jump(128,"SpawnImp")
    TNT1 A 0 A_Jump(96,"SpawnSoul")
    TNT1 A 5 A_SpawnItemEx("Zombieman_2",0,0,0,0,0,0,0,SXF_TELEFRAG|SXF_NOCHECKPOSITION,0)
    stop
  SpawnImp:
    TNT1 A 5 A_SpawnItemEx("DoomImp_Spawner",0,0,0,0,0,0,0,SXF_TELEFRAG|SXF_NOCHECKPOSITION,0)
    stop
  SpawnDemon:
    TNT1 A 5 A_SpawnItemEx("Demon_Spawner",0,0,0,0,0,0,0,SXF_TELEFRAG|SXF_NOCHECKPOSITION,0)
    stop
  SpawnRevenant:
    TNT1 A 5 A_SpawnItemEx("ShotgunGuy_2",0,0,0,0,0,0,0,SXF_TELEFRAG|SXF_NOCHECKPOSITION,0)
    stop
  SpawnSoul:
    TNT1 A 5 A_SpawnItemEx("LostSoul_Spawner",0,0,0,0,0,0,0,SXF_TELEFRAG|SXF_NOCHECKPOSITION,0)
    stop
  }
}
actor StoneKnightCubeSelf
{
  Radius 6
  Height 16
  Speed 2
  Damage (1)
  RenderStyle Normal
  Alpha 1
  SeeSound "brain/cube"
  DeathSound "misc/teleport"
  Projectile
  +RIPPER
  scale 0.75
  reactiontime 20
  +THRUGHOST
  states
  {
  Spawn:
    BOSF ABCD 3 bright
    BOSF A 0 A_Stop
    BOSF ABCD 3 bright A_Countdown
    goto Spawn+5
  Death:
    TNT1 A 0 A_SpawnItemEx("SpawnFire",0,0,0,0,0,0,0,0,0)
    TNT1 A 5 A_SpawnItemEx("StoneKnight",0,0,-64,0,0,0,0,SXF_TELEFRAG|SXF_NOCHECKPOSITION,0)
    stop
  }
}

actor SpawnFire2 : SpawnFire replaces SpawnFire
{
  Height 78
  +NOBLOCKMAP
  +NOGRAVITY
  RenderStyle Add
  States
  {
  Spawn:
    FIRE ABCDEFGH 4 Bright A_Fire
    Stop
  }
}
